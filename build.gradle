buildscript {
    ext {
        kotlin_version = '1.2.50'
        ktor_version = '0.9.2'
        spotify_api_vesrion = '2.0.2'
        jrawVersion = '1.1.0'
        junit_version = '5.2.0'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
    }
}

group 'me.avo'
version '0.7.0'

apply plugin: "com.github.ben-manes.versions"
apply plugin: 'kotlin'

task stage(dependsOn: ['fatJar', 'clean'])
build.mustRunAfter clean

task fatJar(type: Jar) {
    manifest {
        attributes 'Compile-Title': 'spottit',
                'Compile-Version': version,
                'Main-Class': 'me.avo.spottit.MainKt'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/avo/arkenv" }
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    /* API */
    compile "se.michaelthelin.spotify:spotify-web-api-java:$spotify_api_vesrion"
    compile "net.dean.jraw:JRAW:$jrawVersion"

    /* Server */
    compile group: "io.ktor", name: "ktor-server-core", version: ktor_version
    compile group: "io.ktor", name: "ktor-server-cio", version: ktor_version
    compile "io.ktor:ktor-freemarker:$ktor_version"

    /* Util */
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile 'org.kodein.di:kodein-di-generic-jvm:5.0.0'
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compile 'org.yaml:snakeyaml:1.21'
    compile 'com.apurebase:arkenv:1.0.2'
    compile 'org.apache.commons:commons-text:1.4'

    testCompile group: 'org.amshove.kluent', name: 'kluent', version: '1.38'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

test {
    useJUnitPlatform {
        excludeTags 'slow'
        includeEngines 'junit-jupiter'
    }

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }

    failFast = false
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
kotlin {
    experimental {
        coroutines "enable"
    }
}